{- 
  TASK 3 - Find and replace binary substring
  Binary string is represented as a cell linked list: string splitted to chunks,
  first chunk stored to the root cell, next one to the cell in ref and so on;
  each cell can have only one ref. 
  Write the method that find and replaces one flags in the binary string
  with another value. Flags and values can be can be of any length, but
  strictly up to 128 bits. The method must replace every flag it finds.
  Flag and the value to be replaced is guaranteed to be greater than 0.
  Lets give a simple example. We have the target flag 101110101 and the value
  to be written 111111111 as inputs, and a linked list of cells, in which the bit
  value of the first cell ends with ...10100001011, and in the ref we have cell that
  starts with 10101000111111...
  The output should be a linked list where the first
  cell ends with ...10100001111, and the second cell starts with 11111000111111...
-}

() recv_internal() {
}

int uint_bits(int x) asm "UBITSIZE";
(int, int, int, int) all_data_bits(slice data, int n) asm "SDATASIZEQ";
forall X -> tuple cons_new(tuple tail, X head) inline asm(head tail) "CONS";
forall X -> (tuple, ()) ~cons_new(tuple tail, X head) inline asm(head tail) "CONS";
(builder) store_ref_new(builder b, cell c) inline asm(c b) "STREF";

(int) pow2(int e) {
  int n = 2;

  if (e == 0) {
    return 1;
  }

  if (e == 1) {
    return n;
  }

  int p = pow2(e / 2);
  p *= p;
  if ((e % 2) == 1) {
    p *= n;
  }

  return p;
}

int preload_uint_v2(slice data, int bits) {
  int data_bits = data.slice_bits();
  ;; ~dump(data_bits);

  if(data_bits >= bits) {
    ;; ~dump(data.preload_uint(bits));
    return data.preload_uint(bits);
  }
  else {
    int first_part = data.preload_uint(data_bits);
    slice ref = data.preload_ref().begin_parse();
    int second_part = ref.preload_uint_v2(bits - data_bits);
    return (first_part * pow2(bits - data_bits) + second_part);
  }
}

(builder, tuple, int) store_uint_v2(builder result, tuple t_result, int data, int data_bits, int i) {
  ;; ~dump(5);
  int empty_bits = 1023 - result.builder_bits();

  if(empty_bits > data_bits) {
    return (result.store_uint(data, data_bits), t_result, i);
  }
  elseif(empty_bits == data_bits) {
    result~store_uint(data, data_bits);
    return (begin_cell(), t_result.cons_new(result), i + 1);
  }
  else {
    slice s = begin_cell().store_uint(data, data_bits).end_cell().begin_parse();
    int first_part = s~load_uint(empty_bits);
    int second_part = s~load_uint(data_bits - empty_bits);
    result~store_uint(first_part, empty_bits);
    t_result~cons_new(result);
    i += 1;
    return begin_cell().store_uint_v2(t_result, second_part, data_bits - empty_bits, i);
  }
}

(slice, ()) skip_bits_v2(slice data, int bits) {
  int left_data_bits = data.slice_bits();

  if(left_data_bits > bits) {
    return (data.skip_bits(bits), ());
  }
  elseif(left_data_bits == bits) {
    if(data.slice_refs_empty?()) {
      return (begin_cell().end_cell().begin_parse(), ());
    }
    else {
      return (data.preload_ref().begin_parse(), ());
    }
  }
  else {
    return data.preload_ref().begin_parse().skip_bits_v2(bits - left_data_bits);
  }
}

;; testable
(cell) find_and_replace(int flag, int value, cell linked_list) method_id {
  if(linked_list.begin_parse().slice_empty?()) {
    return begin_cell().end_cell();
  }
  int flag_bits = flag.uint_bits();
  int value_bits = value.uint_bits();
  slice slist = linked_list.begin_parse();
  builder result = begin_cell();
  tuple t_result = empty_tuple();
  int i = 0;

  (_, int data_bits, _, _) = slist.all_data_bits(1024);
  while(data_bits >= flag_bits) {
    int load_value = slist.preload_uint_v2(flag_bits);
    if(load_value == flag) {
      (result, t_result, i) = result.store_uint_v2(t_result, value, value_bits, i);
      slist~skip_bits_v2(flag_bits);
      data_bits -= flag_bits;
    }
    else {
      int one_bit = slist.preload_uint_v2(1);
      (result, t_result, i) = result.store_uint_v2(t_result, one_bit, 1, i);
      slist~skip_bits_v2(1);
      data_bits -= 1;
    }
  }

  if(data_bits > 0) {
    int left_value = slist.preload_uint_v2(data_bits);
    (result, t_result, i) = result.store_uint_v2(t_result, left_value, data_bits, i);
  }

  builder last = result; 

  repeat(i) {
    last = t_result~list_next().store_ref_new(last.end_cell());
  }

  return last.end_cell();
}