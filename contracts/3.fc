{- 
  TASK 3 - Find and replace binary substring
  Binary string is represented as a cell linked list: string splitted to chunks,
  first chunk stored to the root cell, next one to the cell in ref and so on;
  each cell can have only one ref. 
  Write the method that find and replaces one flags in the binary string
  with another value. Flags and values can be can be of any length, but
  strictly up to 128 bits. The method must replace every flag it finds.
  Flag and the value to be replaced is guaranteed to be greater than 0.
  Lets give a simple example. We have the target flag 101110101 and the value
  to be written 111111111 as inputs, and a linked list of cells, in which the bit
  value of the first cell ends with ...10100001011, and in the ref we have cell that
  starts with 10101000111111...
  The output should be a linked list where the first
  cell ends with ...10100001111, and the second cell starts with 11111000111111...
-}

() recv_internal() {
}

int uint_bits(int x) asm "UBITSIZE";
(int, int, int, int) all_data_bits(slice data, int n) asm "SDATASIZEQ";
forall X -> tuple cons_new(tuple tail, X head) inline asm(head tail) "CONS";
forall X -> (tuple, ()) ~cons_new(tuple tail, X head) inline asm(head tail) "CONS";
(builder) store_ref_new(builder b, cell c) inline asm(c b) "STREF";

(int) pow2(int e) {
  int n = 2;
  if (e == 0) { return 1; }
  if (e == 1) { return n; }
  int p = pow2(e / 2);
  p *= p;
  if ((e % 2) == 1) {
    p *= n;
  }
  return p;
}

;; testable
(cell) find_and_replace(int flag, int value, cell linked_list) method_id {
  int flag_bits = flag.uint_bits();
  int value_bits = value.uint_bits();
  slice slist = linked_list.begin_parse();
  builder result = begin_cell();
  tuple t_result = empty_tuple();
  int i = 0;
  int load_value = 0;
  int data_bits = 0;
  (_, int full_data_bits, _, _) = slist.all_data_bits(1024);

  while(full_data_bits >= flag_bits) {
    data_bits = slist.slice_bits();
    if(data_bits >= flag_bits) {
      load_value = slist.preload_uint(flag_bits);
    }
    else {
      load_value = (slist.preload_uint(data_bits) * pow2(flag_bits - data_bits) +
                    slist.preload_ref().begin_parse().preload_uint(flag_bits - data_bits));
    }

    if(load_value == flag) {
      int empty_bits = 1023 - result.builder_bits();
      if(empty_bits > value_bits) { result~store_uint(value, value_bits); }
      elseif(empty_bits == value_bits) {
        t_result~cons_new(result.store_uint(value, value_bits));
        result = begin_cell();
        i += 1;
      }
      else {
        t_result~cons_new(result);
        result = begin_cell().store_uint(value, value_bits);
        i += 1;
      }

      if(data_bits > flag_bits) { slist~skip_bits(flag_bits); }
      elseif(data_bits == flag_bits) {
        ifnot(slist.slice_refs_empty?()) { slist = slist.preload_ref().begin_parse(); }
      }
      else { slist = slist.preload_ref().begin_parse().skip_bits(flag_bits - data_bits); }

      full_data_bits -= flag_bits;
    }
    else {
      int empty_bits = 1023 - result.builder_bits();
      if(empty_bits > 1) { result~store_uint(slist.preload_uint(1), 1); }
      elseif(empty_bits == 1) {
        t_result~cons_new(result.store_uint(slist.preload_uint(1), 1));
        result = begin_cell();
        i += 1;
      }
      else {
        t_result~cons_new(result);
        result = begin_cell().store_uint(slist.preload_uint(1), 1);
        i += 1;
      }

      if(data_bits > 1) { slist~skip_bits(1); }
      elseif(data_bits == 1) {
        ifnot(slist.slice_refs_empty?()) { slist = slist.preload_ref().begin_parse(); }
      }
      else { slist = slist.preload_ref().begin_parse().skip_bits(1); }

      full_data_bits -= 1;
    }
  }

  data_bits = slist.slice_bits();
  if(data_bits == full_data_bits) {
    load_value = slist.preload_uint(full_data_bits); 
  }
  else {
    load_value = (slist.preload_uint(data_bits) * pow2(full_data_bits - data_bits) +
                  slist.preload_ref().begin_parse().preload_uint(full_data_bits - data_bits));
  }

  int empty_bits = 1023 - result.builder_bits();
  if(empty_bits > full_data_bits) { result~store_uint(load_value, full_data_bits); }
  elseif(empty_bits == full_data_bits) {
    t_result~cons_new(result.store_uint(load_value, full_data_bits));
    result = begin_cell();
    i += 1;
  }
  else {
    t_result~cons_new(result);
    result = begin_cell().store_uint(load_value, full_data_bits);
    i += 1;
  }

  builder last = result; 
  repeat(i) {
    last = t_result~list_next().store_ref_new(last.end_cell());
  }

  return last.end_cell();
}